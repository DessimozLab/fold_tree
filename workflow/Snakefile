
#once snakemake is installed use the following command to test the struct tree


import snakemake.utils
snakemake.utils.min_version("7.8.0")
snake_dir = workflow.basedir
rootdir = ''.join([ sub + '/' for sub in snake_dir.split('/')[:-1] ] )
print(rootdir)

folders = glob_wildcards("{folders}/identifiers.txt").folders
mattypes = ['fident', 'alntmscore', 'lddt']

foldseekpath = './foldseek/bin/foldseek '
print(folders[0:100] , '...')

rule all:
	input:
	#get all treescore and rf distance files for all alntypes
		expand( "{folder}/RFdistances.json" , folder = folders ),
		expand( "{folder}/{mattype}_treescores_struct_tree.json" , folder = folders , mattype = mattypes  ),
		expand( "{folder}/sequences.aln.fst.nwk.rooted" , folder = folders ),
		expand( "{folder}/treescores_sequences.json" , folder = folders )

rule calc_RF:
	conda:
		"config/snake_tree.yaml"
	input:
		"{folder}/fident_struct_tree.nwk.PP.nwk.rooted" , 
		"{folder}/alntmscore_struct_tree.nwk.PP.nwk.rooted" , 
		"{folder}/lddt_struct_tree.nwk.PP.nwk.rooted" , 
		"{folder}/sequences.aln.fst.nwk.rooted",		
	output:
		"{folder}/RFdistances.json" 
	log:
	    "{folder}/logs/RFdist.log" 
	script:
		"../src/calcRF.py"

rule calc_tax_score:
	conda:
		"config/snake_tree.yaml"
	input:
		"{folder}/sequence_dataset.csv",
		"{folder}/{mattype}_struct_tree.nwk.PP.nwk.rooted"
	output:
		"{folder}/{mattype}_treescores_struct_tree.json"
	log:
	    "{folder}/logs/{mattype}_struct_tree_scoring.log"
	script:
		"../src/calctreescores.py"


rule calc_tax_score_seq:
	conda:
		"config/snake_tree.yaml"
	input:
		"{folder}/sequence_dataset.csv",
		"{folder}/sequences.aln.fst.nwk.rooted"
	output:
		"{folder}/treescores_sequences.json"
	log:
	    "{folder}/logs/sequences_scoring.log"
	script:
		"../src/calctreescores.py"

rule mad_root_struct:
	conda:
		"config/snake_tree.yaml"
	input:
		"{folder}/{mattype}_struct_tree.nwk.PP.nwk"
	output:
		"{folder}/{mattype}_struct_tree.nwk.PP.nwk.rooted"
	log:
	    "{folder}/logs/{mattype}_struct_madroot.log"
	shell:
		rootdir+'madroot/mad {wildcards.folder}/{wildcards.mattype}_struct_tree.nwk.PP.nwk'

rule mad_root_seq:
	conda:
		"config/snake_tree.yaml"
	input:
		"{folder}/sequences.aln.fst.nwk"
	output:
		"{folder}/sequences.aln.fst.nwk.rooted"
	log:
	    "{folder}/logs/madrootseq.log"
	shell:
		rootdir +'madroot/mad  {wildcards.folder}/sequences.aln.fst.nwk'

rule postprocess:
	conda:
		"config/snake_tree.yaml"
	input:
		"{folder}/{mattype}_struct_tree.nwk"
	output:
		"{folder}/{mattype}_struct_tree.nwk.PP.nwk"
	log:
	    "{folder}/logs/{mattype}_struct_postprocess.log"
	script:
		'../src/postprocess.py'

rule fastme:
	conda:
		"config/snake_tree.yaml"
	input:
		"{folder}/{mattype}_fastmemat.txt"
	output:
		"{folder}/{mattype}_struct_tree.nwk"
	log:
	    "{folder}/logs/{mattype}_fastme.log"
	shell:
		'fastme -i {wildcards.folder}/{wildcards.mattype}_fastmemat.txt -o {wildcards.folder}/{wildcards.mattype}_struct_tree.nwk -n '

rule fasttree:
	conda:
		"config/snake_tree.yaml"
	input:
		alignment="{folder}/sequences.aln.fst"
	output:
		tree="{folder}/sequences.aln.fst.nwk"
	log:
	    "{folder}/logs/fasttree.log"
	params:
		extra="",
	wrapper:
		"v1.20.0/bio/fasttree"

rule foldseek2distmat:
	conda:
		"config/snake_tree.yaml"
	input:
		"{folder}/allvall.csv"
	output:
		"{folder}/fident_fastmemat.txt",
		"{folder}/alntmscore_fastmemat.txt",
		"{folder}/lddt_fastmemat.txt",
	log:
	    "{folder}/logs/foldseek2distmat.log"
	script:
		"../src/foldseekres2distmat.py"

rule foldseek_allvall:
	conda:
		"config/snake_tree.yaml"
	input:
		"{folder}/finalset.json"
	output:
		"{folder}/allvall.csv"
	log:
		"{folder}/logs/foldseekallvall.log"
	shell:
		foldseekpath + " easy-search {wildcards.folder}/structs/* {wildcards.folder}/structs/* {wildcards.folder}/allvall.csv {wildcards.folder}/tmp --format-output 'query,target,fident,alnlen,mismatch,gapopen,qstart,qend,tstart,tend,evalue,bits,lddt,lddtfull,alntmscore' --exhaustive-search" 

rule clustalo:
	conda: 
		"config/snake_tree.yaml"
	input:
		"{folder}/sequences.fst",
	output:
		"{folder}/sequences.aln.fst",
	params:
		extra="",
	log:
	    "{folder}/logs/clustalo.log",
	threads: 1
	wrapper:
		"v1.20.0/bio/clustalo"

rule dl_ids_sequences:
	conda: 
		"config/snake_tree.yaml"
	input:
		ids="{folder}/identifiers.txt",
	output:
		"{folder}/sequence_dataset.csv",
	log:
	    "{folder}/logs/dlsequences.log"
	script:
		"../src/dl_sequences.py"

rule plddt:
	conda:
		"config/snake_tree.yaml"
	input:
		"{folder}/finalset.json",
	output:
		"{folder}/plddt.json"
	log:
	    "{folder}/logs/plddt.log"
	script:
		'../src/grabplddt.py'

rule dl_ids_structs:
	conda: 
		"config/snake_tree.yaml"
	input:
		"{folder}/sequence_dataset.csv",
	output:
		"{folder}/sequences.fst",
		"{folder}/finalset.json",
	log:
	    "{folder}/logs/dlstructs.log",
	params:
		extra="", 
	script:
		"../src/dl_structs.py"
